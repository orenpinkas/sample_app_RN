apply plugin: 'com.android.library'
apply plugin: 'maven-publish'
group = 'com.github.outbrain'


tasks.register('sourcesJar', Jar) {
    archiveClassifier.set('sources')
    from android.sourceSets.main.javaDirectories
}

artifacts {
    archives sourcesJar
}


ext {
    libraryName = 'android-sdk'
    libraryGroup = 'com.outbrain.mobile'
    libraryVersion = '4.30.10'
}

apply from: '../cherry_repo_upload.gradle'

android {
    compileSdkVersion rootProject.compileSdkVersion


    testOptions {
        unitTests.returnDefaultValues = true
        unitTests.includeAndroidResources =  true
    }

    defaultConfig {
        minSdkVersion rootProject.minSdkVersion
        targetSdkVersion rootProject.targetSdkVersion
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
        debug {
            // Optionally configure debug build type settings
        }
    }
    namespace 'com.outbrain.OBSDK'

    testOptions {
        unitTests.all {
            // All the usual Gradle options.
            testLogging {
                events "passed", "skipped", "failed", "standardOut", "standardError"
                outputs.upToDateWhen {false}
                showStandardStreams = false
            }
        }
    }
    publishing {
        singleVariant('release') {
            withSourcesJar()
            withJavadocJar()
        }

        singleVariant('debug') {
            withSourcesJar()
            withJavadocJar()
        }
    }
    buildToolsVersion '34.0.0'
    lint {
        abortOnError false
    }
}


dependencies {
    implementation(platform("org.jetbrains.kotlin:kotlin-bom:1.8.22"))

    implementation 'com.outbrain:discrete-scrollview:1.5.1'
    implementation 'com.outbrain:pageindicatorview:1.0.3'

    implementation(platform("com.squareup.okhttp3:okhttp-bom:4.12.0"))

    implementation 'com.squareup.picasso:picasso:2.71828'
    implementation 'com.squareup:otto:1.3.8'
    implementation 'org.apache.commons:commons-text:1.6'
    implementation 'org.greenrobot:eventbus:3.3.1'

    implementation 'com.google.android.gms:play-services-ads-identifier:18.1.0'
    implementation 'androidx.browser:browser:1.8.0'
    implementation 'com.googlecode.json-simple:json-simple:1.1'
    implementation "androidx.recyclerview:recyclerview:1.3.2"
    implementation "androidx.cardview:cardview:1.0.0"
    implementation 'androidx.appcompat:appcompat:1.7.0'
    implementation "androidx.constraintlayout:constraintlayout:2.1.4"

    testImplementation 'junit:junit:4.13.2'
    testImplementation 'androidx.test:core:1.6.1'
    testImplementation 'org.robolectric:robolectric:4.11.1'
    testImplementation 'org.mockito:mockito-core:2.25.0'
    testImplementation 'com.googlecode.json-simple:json-simple:1.1'
    testImplementation 'org.json:json:20180813'
}



afterEvaluate {
    publishing {
        publications {
            // Creates a Maven publication called "release".
            release(MavenPublication) {
                // Applies the component for the release build variant.
                from components.findByName('release')
                pom {
                    name = libraryName
                    packaging = 'jar'
                    licenses {
                        license {
                            name = 'The Apache License, Version 2.0'
                            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }
                }
            }
            // Creates a Maven publication called “debug”.
            debug(MavenPublication) {
                // Applies the component for the debug build variant.
                from components.findByName('debug')

            }
        }
    }
}

